import org.gradle.api.artifacts.*

apply plugin: 'base' // To add "clean" task to the root project.

wrapper {
    gradleVersion = '4.10.2'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

task mergedJavadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    title = 'All modules'
    destinationDir = new File(project.buildDir, 'merged-javadoc')

    // Note: The closures below are executed lazily.
    source {
       subprojects*.sourceSets*.main*.allSource
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve()
    }
}

task publishToMavenLocal() {
    dependsOn ':Shared:publishToMavenLocal'
	dependsOn ':Core:publishToMavenLocal'
	//dependsOn ':GradlePlugin:publishToMavenLocal'
}

task publishToS3() {
    dependsOn ':Shared:publishMavenJavaPublicationToMavenRepository'
	dependsOn ':Core:publishMavenJavaPublicationToMavenRepository'
	//dependsOn ':GradlePlugin:publishToMavenLocal'
}
